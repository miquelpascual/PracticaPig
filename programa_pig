REGISTER /usr/lib/pig/piggybank.jar;
extract_details = LOAD '/user/cloudera/pig_analisis_opinions/critiquescinematografiques.csv' USING org.apache.pig.piggybank.storage.CSVExcelStorage(',', 'YES_MULTILINE')  AS (text:chararray, label:int, id:int);
tokens = foreach extract_details generate id,label,text, FLATTEN(TOKENIZE(text)) As word;
dictionary = load '/user/cloudera/pig_analisis_opinions/AFINN.txt' using PigStorage('\t') AS(word:chararray,rating:int);
word_rating = join tokens by word left outer, dictionary by word using 'replicated';
describe word_rating;
rating = foreach word_rating generate tokens::id as id,tokens::text as text, tokens::label as label, dictionary::rating as rate;
word_group = group rating by (id,text,label);
avg_rate = foreach word_group generate group, AVG(rating.rate) as AVG;
comp5 = foreach avg_rate generate group, (((AVG>=0) AND (group.label==1)) OR ((AVG<0) AND (group.label==0))? 1 : 0) as c:int, AVG;
/* dump comp5; */
/* STORE comp5 INTO '/user/cloudera/WorkspacePigAnalisisOpinionsPractica/resultat_analisis_opinions' 
 USING org.apache.pig.piggybank.storage.CSVExcelStorage(',', 'YES_MULTILINE'); */
 
comp5_grup = GROUP comp5 ALL;

comp5_sol = FOREACH comp5_grup
                   {
                      comp_t = FILTER comp5 BY c == 1;
                      comp_f = FILTER comp5 BY c == 0;

                    GENERATE group, COUNT(comp_t) as comp_t, COUNT(comp_f) as comp_f;
                   };
/* STORE comp5_sol INTO '/user/cloudera/WorkspacePigAnalisisOpinionsPractica/resultat_analisis_opinions_recompte' USING org.apache.pig.piggybank.storage.CSVExcelStorage(',', 'YES_MULTILINE'); */

count_rating= foreach word_group
  {
      positives = FILTER rating BY rate >= 0;
      negatives = FILTER rating BY rate < 0;
      GENERATE group, COUNT(positives) as n_positives, COUNT(negatives) as n_negatives;
  }
rating_g = foreach count_rating generate group.id, group.text, group.label, n_positives, n_negatives;
comp5_g = foreach comp5 generate group.id, group.text, group.label, c;

rating_join = join comp5_g by (id, text, label) left outer, rating_g by (id, text, label) using 'replicated';
rating_final = foreach rating_join generate comp5_g::id as id, comp5_g::text as text, comp5_g::label as label, comp5_g::c as c, rating_g::n_positives as n_positives, rating_g::n_negatives as n_negatives;
STORE rating_final INTO '/user/cloudera/WorkspacePigAnalisisOpinionsPractica/resultat_analisis_opinions_paraules' USING org.apache.pig.piggybank.storage.CSVExcelStorage(',', 'YES_MULTILINE');
